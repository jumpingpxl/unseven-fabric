plugins {
    alias(libraries.plugins.loom) apply false
}

// The version catalogs
val libs = the<org.gradle.accessors.dm.LibrariesForLibraries>()

val accessWidener = lookupAccessWidener()
subprojects {
    // Skip non-mod projects
    if (project.name == "integrations") {
        return@subprojects
    }

    plugins.apply(libs.plugins.loom.get().pluginId)

    repositories {
        maven("https://maven.fabricmc.net/")

        maven("https://api.modrinth.com/maven") {
            content {
                includeGroup("maven.modrinth")
            }
        }

        maven("https://maven.isxander.dev/releases") {
            name = "Xander Maven"
        }
    }

    dependencies {
        annotationProcessor(project(":processor"))

        // Change the Minecraft Version in /gradle/libraries.versions.toml
        configurations["minecraft"]("com.mojang:minecraft:${libs.versions.minecraft.get()}")

        implementation(libs.fabric.loader)
        compileOnly(libs.annotations)
    }

    configure<net.fabricmc.loom.api.LoomGradleExtensionAPI> {
        if (accessWidener != null) {
            accessWidenerPath = accessWidener
        }

        runs.clear()
    }

    tasks.compileJava {
        options.compilerArgs.add("-AmoduleName=" + project.name)
        options.compilerArgs.add("-AprojectId=" + rootProject.name)
        options.compilerArgs.add("-AjavaVersion=" + globalSettings.targetJavaVersion)
    }
}

fun lookupAccessWidener(): File? {
    val resourcesDirectory = file("core/src/main/resources/")
    val accessWideners = resourcesDirectory.listFiles().filter { file -> file.extension == "accesswidener" }
    if (accessWideners.size > 1) {
        error("Multiple access wideners found.")
    }

    return accessWideners.firstOrNull()
}